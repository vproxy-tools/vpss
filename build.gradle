plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "application"
    id "idea"
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
}

group 'io.vproxy'
version loadVersion()

mainClassName = 'io.vproxy.vpss.Main'

shadowJar {
    archiveName = 'vpss.jar'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

sourceCompatibility = 11.0
targetCompatibility = 11.0
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

compileKotlin {
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += '-Xjvm-default=enable'
        freeCompilerArgs += '-Xassertions=jvm'
    }
    destinationDir = compileJava.destinationDir
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += '-Xjvm-default=enable'
    }
}

compileJava {
    doFirst {
        options.compilerArgs << '--module-path' << classpath.asPath
    }
    options.compilerArgs << '--add-modules' << [
            'io.vproxy.app',
            'io.vproxy.core',
            'io.vproxy.lib',
            'io.vproxy.base',
            'kotlin.stdlib',
            'kotlinx.coroutines.core.jvm',
    ].join(',')
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: '1.5.31'
    implementation group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core-jvm', version: '1.5.2'
    compileOnly group: 'io.vproxy', name: 'vproxy-all', version: 'MAVEN-LOCAL'
    runtimeOnly files('./vproxy-no-kt-runtime.jar')
}

def loadVersion() {
    def PREFIX = "const val VERSION = \""
    def SUFFIX = "\" // _THE_VERSION_"
    def ver = file(projectDir.getAbsolutePath() + "/src/main/kotlin/io/vproxy/vpss/util/Consts.kt")
    def lines = ver.getText().split("\n")
    for (def line : lines) {
        line = line.trim()
        if (line.startsWith(PREFIX) && line.endsWith(SUFFIX)) {
            return line.substring(PREFIX.length(), line.length() - SUFFIX.length())
        }
    }
    return "unknown"
}
